            .include MACROS.inc

            .org 0

Z:          .data 0 
PLUS1:      .data 1 
MINUS1:     .data (-1)
            JMP Start

tmpA:       .data 0             ; Used by macros
tmpB:       .data 0             ; Used by macros
tmpC:       .data 0             ; Used by macros

atoiVal:        .data 0         ; Result from ATOI operation
compResult:     .data 0         ; Result from string compare operations
pFound:         .data 0         ; Result from FindLine operation

lineNo:         .data 0         ; The current line number executing
offsetNextLine: .data 0         ; Offset to the next line
addrNextLine:   .data 0         ; The computed address of the next line

bitopResult:    .data 0         ; Result from bit operations

program_:   .data program       ; program_ is a constant holding the address of the program array

seed:            .data 0x1        ; Seed for the first random number generator
rndVal:          .data 0          ; Random number generated

mu1:            .data 0
mu2:            .data 0

;-----------------------------------------------------------------------------
; START OF CODE
;-----------------------------------------------------------------------------

Start:
        MOV stack_ SP                              ; Initialize the stack pointer

        PUSH msgLogo
        CALL PrintString
        DEBUGPRINT "[Debug mode enabled]\r\n"


;Lupe:
;        PUSH CONST_10
;        CALL Prng ; Random
;        PUSH seed
;        CALL PrintHex
;        PRINTCRLF
;        JMP Lupe        

;-----------------------------------------------------------------------------
; COMMAND LINE PROMPT WITH RESETTING THE STACK AFTER ERROR
;-----------------------------------------------------------------------------

Restart:
        MOV program_ pCode
        MOV stack_ SP                              ; Initialize the stack pointer

;-----------------------------------------------------------------------------
; COMMAND LINE PROMPT
;-----------------------------------------------------------------------------

Prompt:
        PRINTCRLF
        OUT ASCII_GREATER

        CALL InputString        
        MOV buf pCode
        CALL GetParsedToken
        CMPEQ tokenType CONST_0 Prompt          ; Empty line, prompt again

;       PRINTCRLF                    ;debug
;       CALL debugToken              ;debug

        CMPEQ tokenType CONST_1 Editor          ; Starts with a number, so it's an editor command
        PRINTCRLF
        CMPEQ tokenValue kwLIST PromptLIST      ; LIST command
        CMPEQ tokenValue kwRUN PromptRUN        ; RUN command
        CMPEQ tokenValue kwNEW cmdNEW           ; NEW command
        PUSH msgInvalidPrompt                   ; All other things are invalid
        JMP SyntaxError

;-----------------------------------------------------------------------------
SyntaxError:
;-----------------------------------------------------------------------------

        PUSH msgSyntaxError
        CALL PrintString
        CALL PrintString
        JMP Restart

;-----------------------------------------------------------------------------
PromptRUN:
;-----------------------------------------------------------------------------
        CALL GetParsedToken                       ; Get the line number to start running from
        PUSH tokenValue
        CALL FindLine
        MOV pFound pCode

ProgramLine:
;        PUSH pCode                      ;debug
;        CALL PrintHex                      ;debug
;        OUT ASCII_SPACE                     ;debug

        CLR tokenType                       ; Clear the token type so we don't trigger the EOL check in the loop
        INDEXEDRD pCode lineNo           ; Get Linenumber of current line as a binary value
        JMP0 lineNo cmdEND                  ; If we reach the end of the code it's an implicit END instruction
        INC pCode

        INDEXEDRD pCode offsetNextLine   ; Get offset to next line
        MOV pCode addrNextLine           ; Calculate the address of the next line by adding the offset to the current location
        ADD offsetNextLine addrNextLine     
        INC addrNextLine

        INC pCode

;        CALL debugLine             ;debug

Statement:
        CALL GetParsedToken
;       CALL debugToken             ;debug

        CMPEQ tokenType CONST_0 ProgramLine     ; End-Of-Line?
        CMPNE tokenType CONST_5 notMisc
        CMPEQ tokenValue ASCII_COLON Statement   ; End-Of-Statement?
        JMP SyntaxError        

notMisc:
        ; Check for keywords and handle them
        CMPEQ tokenValue kwEND cmdEND
        CMPEQ tokenValue kwREM cmdREM
        CMPEQ tokenValue kwPRINT cmdPRINT
        CMPEQ tokenValue kwGOTO cmdGOTO
        CMPEQ tokenValue kwLET cmdLET
        CMPEQ tokenValue kwGOSUB cmdGOSUB
        CMPEQ tokenValue kwRETURN cmdRETURN
        CMPEQ tokenValue kwLIST cmdLIST
        CMPEQ tokenValue kwIF cmdIF
        CMPEQ tokenValue kwFOR cmdFOR
        CMPEQ tokenValue kwNEXT cmdNEXT
        CMPEQ tokenValue kwINPUT cmdINPUT
        CMPEQ tokenValue kwPRHEX cmdPRHEX
        JMP SyntaxError




;-----------------------------------------------------------------------------
; MESSAGE STRINGS
;-----------------------------------------------------------------------------

msgLogo:        
        .data $+1
        .data "\r\n"
        .data " __        __        ___  __     ___                __        __     __  "       ,"\r\n"
        .data "/__` |  | |__) |    |__  /  \\     |  | |\\ | \\ /    |__)  /\\  /__` | /  ` "       ,"\r\n"
        .data ".__/ \\__/ |__) |___ |___ \\__\\\\    |  | | \\|  |     |__) /--\\ .__/ | \\__, "       ,"\r\n"
        .data "\r\n"
        .data "[[ version 0.02 - (c) 2024 Mats Engstrom - github.com/mengstr/SUBASIC ]]\r\n", 0
        .data "\r\n"
        .data 0                                         

msgHaltedByEnd:         .data $+1, "\r\n", 0
;msgHaltedByEnd:         .data $+1, "\r\nExecution halted by END\r\n", 0
msgEndOfFile:           .data $+1, "\r\nExecution halted by End Of File\r\n", 0
msgLineNotFound:        .data $+1, "\r\nLine not found\r\n", 0
msgSyntaxError:         .data $+1, "\r\nSyntax error: ", 0
msgUnknownCommand:      .data $+1, "Unknown command", 0
msgInvalidToken:        .data $+1, "Invalid token,0
msgInvalidPrompt:       .data $+1, "Invalid command",0
msgInvalidOperator:     .data $+1, "Invalid operator", 0
msgInvalidTokenType:    .data $+1, "Invalid token type", 0

;-----------------------------------------------------------------------------
; INCLUDE FILES
;-----------------------------------------------------------------------------

        ;
        ; Standard SUBLEQ macros and functions
        ;

        .include IO.inc
        .include STRING.inc
        .include ATOI.inc
        .include RANDOM.inc
        .include CONSTANTS.inc
        .include STACK.inc

        ;
        ; Subroutines for the BASIC commands
        ;

        .include cmdEND.inc
        .include cmdINPUT.inc
        .include cmdREM.inc
        .include cmdPRINT.inc
        .include cmdGOTO.inc
        .include cmdIF.inc
        .include cmdLET.inc
        .include cmdGOSUB.inc
        .include cmdRETURN.inc
        .include cmdFOR.inc
        .include cmdNEXT.inc
        .include cmdLIST.inc
        .include cmdNEW.inc

        ;
        ; Subroutines for the BASIC utilities
        ;

        .include EDITOR.inc
        .include KEYBOARD.inc
        .include FINDLINE.inc
        .include PARSEEXPRESSION.inc
        .include GETTOKEN.inc
        .include VARIABLES.inc
        .INCLUDE BITOPS.inc
        .include debug.inc


;-----------------------------------------------------------------------------
; BASIC PROGRAM SOURCE CODE
;-----------------------------------------------------------------------------

;        .org 0x7000

program:                                ; The program source code to be run
L5:  .data 5,  L7-$-1, "REM This is a :test:"                    ,0
L7:  .data 7,  L10-$-1, "PRINT"                                 ,0
L10: .data 10, L20-$-1, "PRINT 1"                                 ,0      
L20: .data 20, L30-$-1, "PRINT A"                                 ,0
L30: .data 30, L40-$-1, "PRINT \"Hello\""                         ,0
L40: .data 40, L50-$-1, "PRINT 2;"                                ,0
L50: .data 50, L60-$-1, "PRINT B;"                                ,0
L60: .data 60, L65-$-1, "PRINT \"World\";"                        ,0
L65: .data 65, L70-$-1, "PRINT"                                   ,0
L70: .data 70, L80-$-1, "PRINT A;3;\"foo\";B;4;\"bar\""           ,0
L80: .data 80, Lend-$-1,"END"                                    ,0
Lend:.data 0
